import org.apache.commons.codec.binary.Base64

plugins {
    id "com.android.application"
    id "kotlin-android"
    id "dev.flutter.flutter-gradle-plugin"
}

def localProperties = new Properties()
def localPropertiesFile = rootProject.file('local.properties')
if (localPropertiesFile.exists()) {
    localPropertiesFile.withReader('UTF-8') { reader ->
        localProperties.load(reader)
    }
}

def flutterVersionCode = localProperties.getProperty('flutter.versionCode')
if (flutterVersionCode == null) {
    flutterVersionCode = '1'
}

def flutterVersionName = localProperties.getProperty('flutter.versionName')
if (flutterVersionName == null) {
    flutterVersionName = '1.0'
}

def keystoreProperties = new Properties()
def keystorePropertiesFile = rootProject.file('key.properties')
if (keystorePropertiesFile.exists()) {
  keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}

def dartEnvVars = [
    APP_ID_SUFFIX: null,
    VERSION_NAME_SUFFIX: null,
    APP_NAME_PREFIX: ''
];
if (project.hasProperty('dart-defines')) {
    dartEnvVars = dartEnvVars + project.property('dart-defines')
        .split(',')
        .collectEntries { entry ->
            def entry_bytes = new Base64().decode(entry)
            def entry_string = new String(entry_bytes, "UTF8")
            def pair = entry_string.split('=')
            [(pair.first()): pair.last()]
        }
}

android {
    compileSdkVersion 35

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    lintOptions {
        disable 'InvalidPackage'
    }

    defaultConfig {
        applicationId "de.lilaherzprojekt.lilafestivalapp"
        namespace "de.lilaherzprojekt.lilafestivalapp"
        applicationIdSuffix dartEnvVars.APP_ID_SUFFIX
        versionNameSuffix dartEnvVars.VERSION_NAME_SUFFIX
        minSdkVersion flutter.minSdkVersion
        targetSdkVersion 35
        versionCode flutterVersionCode.toInteger()
        versionName flutterVersionName
    }

    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_11.toString()
    }

    flavorDimensions "festival"
    productFlavors {
        party_san {
            dimension "festival"
            applicationId "de.lilaherzprojekt.party_san"
            resValue "string", "app_name", dartEnvVars.APP_NAME_PREFIX + "Party.San"
        }

        spirit {
            dimension "festival"
            applicationId "de.lilaherzprojekt.spirit"
            resValue "string", "app_name", dartEnvVars.APP_NAME_PREFIX + "Spirit"
        }
    }

    signingConfigs {
       release {
           keyAlias keystoreProperties['keyAlias']
           keyPassword keystoreProperties['keyPassword']
           storeFile file(keystoreProperties['storeFile'])
           storePassword keystoreProperties['storePassword']
       }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release

            shrinkResources true
            minifyEnabled true

            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
}

flutter {
    source '../..'
}

dependencies {
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:2.1.4'
}
